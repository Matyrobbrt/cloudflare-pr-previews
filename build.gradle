plugins {
  id 'java'
  id 'org.groovymc.simpleci' version '0.2.3'
  id 'com.github.johnrengelman.shadow' version '7.1.2'
}

versioning {
  fromTag = '1.0'
}
version = versioning.calculatedVersion

repositories {
  mavenCentral()
}

dependencies {
  compileOnly 'org.jetbrains:annotations:23.1.0'
  implementation('org.kohsuke:github-api:1.315') {
    exclude module: 'snakeyaml'
  }
  implementation 'org.yaml:snakeyaml:2.0'
  implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
  implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3'

  implementation 'io.jsonwebtoken:jjwt-api:0.10.5'
  implementation 'io.jsonwebtoken:jjwt-impl:0.10.5'
  implementation 'io.jsonwebtoken:jjwt-jackson:0.10.5'
}

shadowJar {
  manifest.attributes([
          'Main-Class': 'com.matyrobbrt.actions.cfprpreviews.CloudflarePRPreviews'
  ])
  destinationDirectory = file('dist')
  archiveFileName = 'app.jar'
}

tasks.named('compileJava', JavaCompile).configure {
  options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.register('outputVersion') {
  doLast {
    file('envs').text = "VERSION=${project.version}"
    file('action.yml').text = file('action.yml').text.replace('\'docker://ghcr.io/matyrobbrt/cloudflare-pr-previews\'', "'docker://ghcr.io/matyrobbrt/cloudflare-pr-previews:${project.version}'")
  }
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(17))
  }
}